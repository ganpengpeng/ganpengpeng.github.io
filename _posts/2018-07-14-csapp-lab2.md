---
layout: post
title:  "csapp lab2:二进制炸弹"
categories: 系统基础
tags:  csapp gdb bomblab 汇编
author: ganpeng
---

* content
{:toc}


这次的实验是一个拆二进制炸弹的实验，每个阶段都需要拆弹人输入特定的信息。如果信息正确则炸弹拆除，否则boom～～～

本次实验需要使用以下工具，因此可能需要学习一下工具的使用：
- gdb：用于命令行调试。参考资料：[100个gdb小技巧](https://legacy.gitbook.com/book/wizardforcel/100-gdb-tips/details)。
- objdump：反汇编



实验程序和资料可在[此处](https://github.com/ganpengpeng/csapp_labs)取得。

## phase1

首先执行炸弹文件看一下提示吧。

![prompt](/static/bomb_prompt.png)

从上面并没有得到什么信息，再来看一下bomb.c文件的main函数。

```cpp
#include <stdio.h>
#include <stdlib.h>
#include "support.h"
#include "phases.h"

FILE *infile;

int main(int argc, char *argv[])
{
    char *input;

    /* Note to self: remember to port this bomb to Windows and put a 
     * fantastic GUI on it. */

    /* When run with no arguments, the bomb reads its input lines 
     * from standard input. */
    if (argc == 1) {  
	infile = stdin;
    } 

    /* When run with one argument <file>, the bomb reads from <file> 
     * until EOF, and then switches to standard input. Thus, as you 
     * defuse each phase, you can add its defusing string to <file> and
     * avoid having to retype it. */
    else if (argc == 2) {
	if (!(infile = fopen(argv[1], "r"))) {
	    printf("%s: Error: Couldn't open %s\n", argv[0], argv[1]);
	    exit(8);
	}
    }

    /* You can't call the bomb with more than 1 command line argument. */
    else {
	printf("Usage: %s [<input_file>]\n", argv[0]);
	exit(8);
    }

    /* Do all sorts of secret stuff that makes the bomb harder to defuse. */
    initialize_bomb();

    printf("Welcome to my fiendish little bomb. You have 6 phases with\n");
    printf("which to blow yourself up. Have a nice day!\n");

    /* Hmm...  Six phases must be more secure than one phase! */
    input = read_line();             /* Get input                   */
    phase_1(input);                  /* Run the phase               */
    phase_defused();                 /* Drat!  They figured it out!
				      * Let me know how they did it. */
    printf("Phase 1 defused. How about the next one?\n");

    /* The second phase is harder.  No one will ever figure out
     * how to defuse this... */
    input = read_line();
    phase_2(input);
    phase_defused();
    printf("That's number 2.  Keep going!\n");

    /* I guess this is too easy so far.  Some more complex code will
     * confuse people. */
    input = read_line();
    phase_3(input);
    phase_defused();
    printf("Halfway there!\n");

    /* Oh yeah?  Well, how good is your math?  Try on this saucy problem! */
    input = read_line();
    phase_4(input);
    phase_defused();
    printf("So you got that one.  Try this one.\n");
    
    /* Round and 'round in memory we go, where we stop, the bomb blows! */
    input = read_line();
    phase_5(input);
    phase_defused();
    printf("Good work!  On to the next...\n");

    /* This phase will never be used, since no one will get past the
     * earlier ones.  But just in case, make this one extra hard. */
    input = read_line();
    phase_6(input);
    phase_defused();

    /* Wow, they got it!  But isn't something... missing?  Perhaps
     * something they overlooked?  Mua ha ha ha ha! */
    
    return 0;
}
```

整个main函数很简单，共六个炸弹，每次都先读取一行，然后送入phase_X函数中进行判断。如果通过则执行phase_defused函数打印相应提示，否则直接在phase_X函数中引爆炸弹。

因此重点就是phase_X函数。接下来一个一个解决。

首先用 **objdump -d bomb > asm** 命令将bomb反汇编到asm文件中。

然后用gdb对bomb可执行文件进行调试。用 **b main** 命令在main函数处设置断点，然后用 **r** 命令执行。

![main](/static/gdb1.png)

在asm文件中找到phase_1代码。

通过分析这段代码可以知道程序先将 **0x402400** 这个地址送到了源地址寄存器esi中，然后调用了 **strings_not_equal** 这个函数,接着用 **test** 命令进行了测试，当字符串相同时用 **je** 命令跳出函数，否则执行 **explode_bomb** 函数，因此不难猜测0x402400这个地址有我们想要的东西。

```
0000000000400ee0 <phase_1>:
  400ee0:	48 83 ec 08          	sub    $0x8,%rsp
  400ee4:	be 00 24 40 00       	mov    $0x402400,%esi //比较0x402400这个地址的字符串
  400ee9:	e8 4a 04 00 00       	callq  401338 <strings_not_equal>
  400eee:	85 c0                	test   %eax,%eax
  400ef0:	74 05                	je     400ef7 <phase_1+0x17>
  400ef2:	e8 43 05 00 00       	callq  40143a <explode_bomb>
  400ef7:	48 83 c4 08          	add    $0x8,%rsp
  400efb:	c3                   	retq   
```

在刚才的gdb界面使用 **x/s 0x402400** 以字符串形式打印出这个地址的内容。结果如下：

![0x402400](/static/phase1_solution.png)

执行bomb文件并输入这串字符串。

![phase1_defused](/static/phase1_defused.png)

第一个炸弹拆除。

## phase2

第二个炸弹汇编代码如下。

```
0000000000400efc <phase_2>:
  400efc:	55                   	push   %rbp
  400efd:	53                   	push   %rbx
  400efe:	48 83 ec 28          	sub    $0x28,%rsp
  400f02:	48 89 e6             	mov    %rsp,%rsi
  400f05:	e8 52 05 00 00       	callq  40145c <read_six_numbers>
  400f0a:	83 3c 24 01          	cmpl   $0x1,(%rsp) //将第一个数和1比较
  400f0e:	74 20                	je     400f30 <phase_2+0x34>
  400f10:	e8 25 05 00 00       	callq  40143a <explode_bomb>
  400f15:	eb 19                	jmp    400f30 <phase_2+0x34>
  400f17:	8b 43 fc             	mov    -0x4(%rbx),%eax //将输入的第一个数送到eax
  400f1a:	01 c0                	add    %eax,%eax //第一个数乘以2
  400f1c:	39 03                	cmp    %eax,(%rbx) //和第二个数比较
  400f1e:	74 05                	je     400f25 <phase_2+0x29>
  400f20:	e8 15 05 00 00       	callq  40143a <explode_bomb>
  400f25:	48 83 c3 04          	add    $0x4,%rbx //将rbx指针移到下一个数
  400f29:	48 39 eb             	cmp    %rbp,%rbx //是否达到最后一个数
  400f2c:	75 e9                	jne    400f17 <phase_2+0x1b>
  400f2e:	eb 0c                	jmp    400f3c <phase_2+0x40>
  400f30:	48 8d 5c 24 04       	lea    0x4(%rsp),%rbx
  400f35:	48 8d 6c 24 18       	lea    0x18(%rsp),%rbp
  400f3a:	eb db                	jmp    400f17 <phase_2+0x1b>
  400f3c:	48 83 c4 28          	add    $0x28,%rsp
  400f40:	5b                   	pop    %rbx
  400f41:	5d                   	pop    %rbp
  400f42:	c3                   	retq   
```

使用gdb对代码进行单步调试之后发现代码的运行过程就如注释中的那样，依次判断前一个数的两倍等不等于后一个数。
- 相等，则继续判断，直到最后一个数。
- 不相等，则炸弹爆炸。

且在 **400f0a** 处代码判断第一个数是否等于1，因此答案就是 **1 2 4 8 16 32**。

![phase2_defused](/static/phase2_defused.png)


## phase3

第三个炸弹代码如下。

```
0000000000400f43 <phase_3>:
  400f43:	48 83 ec 18          	sub    $0x18,%rsp
  400f47:	48 8d 4c 24 0c       	lea    0xc(%rsp),%rcx
  400f4c:	48 8d 54 24 08       	lea    0x8(%rsp),%rdx
  400f51:	be cf 25 40 00       	mov    $0x4025cf,%esi
  400f56:	b8 00 00 00 00       	mov    $0x0,%eax
  400f5b:	e8 90 fc ff ff       	callq  400bf0 <__isoc99_sscanf@plt>
  400f60:	83 f8 01             	cmp    $0x1,%eax
  400f63:	7f 05                	jg     400f6a <phase_3+0x27>
  400f65:	e8 d0 04 00 00       	callq  40143a <explode_bomb>
  400f6a:	83 7c 24 08 07       	cmpl   $0x7,0x8(%rsp)       //第一个数不能大于7
  400f6f:	77 3c                	ja     400fad <phase_3+0x6a>
  400f71:	8b 44 24 08          	mov    0x8(%rsp),%eax
  400f75:	ff 24 c5 70 24 40 00 	jmpq   *0x402470(,%rax,8)   //rax为第一个数
                                                            //跳转到(0x402470+rax*8)这个地址处的内容
  400f7c:	b8 cf 00 00 00       	mov    $0xcf,%eax           //跳转点1
  400f81:	eb 3b                	jmp    400fbe <phase_3+0x7b>
  400f83:	b8 c3 02 00 00       	mov    $0x2c3,%eax          //跳转点2
  400f88:	eb 34                	jmp    400fbe <phase_3+0x7b>
  400f8a:	b8 00 01 00 00       	mov    $0x100,%eax          //跳转点3
  400f8f:	eb 2d                	jmp    400fbe <phase_3+0x7b>
  400f91:	b8 85 01 00 00       	mov    $0x185,%eax          //跳转点4
  400f96:	eb 26                	jmp    400fbe <phase_3+0x7b>
  400f98:	b8 ce 00 00 00       	mov    $0xce,%eax           //跳转点5
  400f9d:	eb 1f                	jmp    400fbe <phase_3+0x7b>
  400f9f:	b8 aa 02 00 00       	mov    $0x2aa,%eax          //跳转点6
  400fa4:	eb 18                	jmp    400fbe <phase_3+0x7b>
  400fa6:	b8 47 01 00 00       	mov    $0x147,%eax          //跳转点7
  400fab:	eb 11                	jmp    400fbe <phase_3+0x7b>
  400fad:	e8 88 04 00 00       	callq  40143a <explode_bomb>
  400fb2:	b8 00 00 00 00       	mov    $0x0,%eax
  400fb7:	eb 05                	jmp    400fbe <phase_3+0x7b>
  400fb9:	b8 37 01 00 00       	mov    $0x137,%eax
  400fbe:	3b 44 24 0c          	cmp    0xc(%rsp),%eax//将输入的第二个数和eax比较
  400fc2:	74 05                	je     400fc9 <phase_3+0x86>
  400fc4:	e8 71 04 00 00       	callq  40143a <explode_bomb>
  400fc9:	48 83 c4 18          	add    $0x18,%rsp
  400fcd:	c3                   	retq
```

第三个炸弹比第二个难一些。

首先直接看不出输入是什么。第二个炸弹可以通过 **read_six_numbers** 这个函数看出需要输入6个数字。

而这里并没有直接的提示输入是一个单词，字符串，或者数字。但是代码中 **400f5b** 调用了 **sscanf** 函数，在这之前有一条指令值得注意，**400f51** 处的 **mov    $0x4025cf,%esi** 。

使用gdb以字符串形式打印 **0x4025cf** 处的内容。

![phase3_input](/static/phase3_input_format.png)

从gdb的输出知道sscanf函数的格式化字符串是 __"%d %d"__，因此第三个炸弹需要输入两个数字。

任选两个数输入(第一个数不能大于7)，这里选了1和2。根据公式算出结果为 __0x402470+1*8=0x402478(rax=1)__ ，在gdb下使用 **x/1xw 0x402478** 以16进制打印该地址开始的1个字类型(32位)。

![jump1](/static/jump1.png)

地址 **0x402478** 处的值为 **0x00400fb9** ，而这个地址处的命令是 **mov $0x137, %eax** ，因此，第二个数应该输入 **0x137(311)**。

所以答案之一为1 311。另外将第一个数从0到6跳转情况截图如下。

![jump_all](/static/jump_all.png)

根据跳转情况得到第三个炸弹所有解如下：

| 第一个数 | 跳转地址 | 第二个数   |
| :------: | :------: | :--------: |
| 0        | 400f7c   | 207(0xcf)  |
| 1        | 400fb9   | 311(0x137) |
| 2        | 400f83   | 707(0x2c3) |
| 3        | 400f8a   | 256(0x100) |
| 4        | 400f91   | 389(0x185) |
| 5        | 400f98   | 206(0xce)  |
| 6        | 400f9f   | 682(0x2aa) |


## phase4

第四个炸弹代码。

```
000000000040100c <phase_4>:
  40100c:	48 83 ec 18          	sub    $0x18,%rsp
  401010:	48 8d 4c 24 0c       	lea    0xc(%rsp),%rcx
  401015:	48 8d 54 24 08       	lea    0x8(%rsp),%rdx
  40101a:	be cf 25 40 00       	mov    $0x4025cf,%esi
  40101f:	b8 00 00 00 00       	mov    $0x0,%eax
  401024:	e8 c7 fb ff ff       	callq  400bf0 <__isoc99_sscanf@plt>
  401029:	83 f8 02             	cmp    $0x2,%eax
  40102c:	75 07                	jne    401035 <phase_4+0x29>
  40102e:	83 7c 24 08 0e       	cmpl   $0xe,0x8(%rsp)//第一个数必须小于等于0xe
  401033:	76 05                	jbe    40103a <phase_4+0x2e>
  401035:	e8 00 04 00 00       	callq  40143a <explode_bomb>
  40103a:	ba 0e 00 00 00       	mov    $0xe,%edx
  40103f:	be 00 00 00 00       	mov    $0x0,%esi
  401044:	8b 7c 24 08          	mov    0x8(%rsp),%edi
  401048:	e8 81 ff ff ff       	callq  400fce <func4>
  40104d:	85 c0                	test   %eax,%eax//func4返回时eax必须是0
  40104f:	75 07                	jne    401058 <phase_4+0x4c>
  401051:	83 7c 24 0c 00       	cmpl   $0x0,0xc(%rsp)//第二个数必须是0
  401056:	74 05                	je     40105d <phase_4+0x51>
  401058:	e8 dd 03 00 00       	callq  40143a <explode_bomb>
  40105d:	48 83 c4 18          	add    $0x18,%rsp
  401061:	c3                   	retq  
  ```

和第三个炸弹一样打印 **0x4025cf** 处的值(同一个值)，同样也是需要输入两个数。

从代码中还能得到几个重要信息：
- **40102e** 处指令要求第一个数小于等于0xe(14)。
- **40104d** 处，func4函数返回值必须是0。
- **401051** 处，第二个数必须是0。

在有了上面几个信息之后不难直接把答案试出来，第一个数从0开始一直到14，第二个数为0。

![phase4_defused](/static/phase4_defused.png)

其实输入 **0 0** 之后这个炸弹就拆除了。不过还是把func4函数的逻辑分析一下。func4函数代码如下：

```
0000000000400fce <func4>:
  400fce:	48 83 ec 08          	sub    $0x8,%rsp
  400fd2:	89 d0                	mov    %edx,%eax //edx初值为0xe
  400fd4:	29 f0                	sub    %esi,%eax //esi初值为0
  400fd6:	89 c1                	mov    %eax,%ecx
  400fd8:	c1 e9 1f             	shr    $0x1f,%ecx
  400fdb:	01 c8                	add    %ecx,%eax
  400fdd:	d1 f8                	sar    %eax
  400fdf:	8d 0c 30             	lea    (%rax,%rsi,1),%ecx
  400fe2:	39 f9                	cmp    %edi,%ecx //edi为第一个数
  400fe4:	7e 0c                	jle    400ff2 <func4+0x24>
  400fe6:	8d 51 ff             	lea    -0x1(%rcx),%edx
  400fe9:	e8 e0 ff ff ff       	callq  400fce <func4>
  400fee:	01 c0                	add    %eax,%eax
  400ff0:	eb 15                	jmp    401007 <func4+0x39>
  400ff2:	b8 00 00 00 00       	mov    $0x0,%eax
  400ff7:	39 f9                	cmp    %edi,%ecx
  400ff9:	7d 0c                	jge    401007 <func4+0x39>
  400ffb:	8d 71 01             	lea    0x1(%rcx),%esi
  400ffe:	e8 cb ff ff ff       	callq  400fce <func4>
  401003:	8d 44 00 01          	lea    0x1(%rax,%rax,1),%eax
  401007:	48 83 c4 08          	add    $0x8,%rsp
  40100b:	c3                   	retq   
```

经过使用gdb实际调试和对代码的分析，func4函数是一个二分查找的过程，其流程如下：
- esi为左边界(初始为0)，edx为右边界(初始为14)
- **400fd2** 到 **400fdf** 执行 **ecx = (edx-esi)/2+esi**
- 如果ecx大于第一个数，则 **400fe6** 处代码将 **ecx-1** 赋给edx,接着递归调用func4函数。
- 如果ecx等于第一个数，则将eax赋值为0并返回。
- 如果ecx小于第一个数，则 **400ffb** 处代码将 **ecx+1** 赋给esi,接着递归调用func4函数。

重点在 **400fee** 和 **401003** 处的指令。在func4函数返回后，**400fee**处指令会将eax乘以2，**401003** 处指令会将eax乘以2再加1。

而phase_4代码给出的信息中要求func4函数返回phase_4的返回值为0，也就是说func4函数返回phase_4时eax必须是0。**这意味着在二分查找的过程中程序不能执行401003处的指令，也就是说二分查找的过程中不能发生ecx大于第一个数的情况。否则eax会乘以2加1，这时eax非零，之后无论是对eax乘以2，还是乘以2后再加1，eax都不可能变回0**

**换个通俗的话来讲，在二分查找的过程中不能出现要查找的值大于中间值的情况**

- 二分查找区间为0~14，第一次的中间值为7;
- 接着区间变为0~6，中间值为3;
- 区间变为0~2，中间值为1;
- 区间变为0~0，结束。

因此第一个数的合法值为7,3,1,0。而第二个数必须为0。

到这里第四个炸弹拆除。

## phase5

代码如下：

```
0000000000401062 <phase_5>:
  401062:	53                   	push   %rbx
  401063:	48 83 ec 20          	sub    $0x20,%rsp
  401067:	48 89 fb             	mov    %rdi,%rbx
  40106a:	64 48 8b 04 25 28 00 	mov    %fs:0x28,%rax
  401071:	00 00 
  401073:	48 89 44 24 18       	mov    %rax,0x18(%rsp)
  401078:	31 c0                	xor    %eax,%eax
  40107a:	e8 9c 02 00 00       	callq  40131b <string_length>
  40107f:	83 f8 06             	cmp    $0x6,%eax//输入一个长度为6的字符串
  401082:	74 4e                	je     4010d2 <phase_5+0x70>
  401084:	e8 b1 03 00 00       	callq  40143a <explode_bomb>
  401089:	eb 47                	jmp    4010d2 <phase_5+0x70>
  40108b:	0f b6 0c 03          	movzbl (%rbx,%rax,1),%ecx//ecx依次遍历6个字符
  40108f:	88 0c 24             	mov    %cl,(%rsp)
  401092:	48 8b 14 24          	mov    (%rsp),%rdx
  401096:	83 e2 0f             	and    $0xf,%edx//取字符低四位当做数组下标
  401099:	0f b6 92 b0 24 40 00 	movzbl 0x4024b0(%rdx),%edx//maduiersnfotvbyl
  4010a0:	88 54 04 10          	mov    %dl,0x10(%rsp,%rax,1)
  4010a4:	48 83 c0 01          	add    $0x1,%rax
  4010a8:	48 83 f8 06          	cmp    $0x6,%rax
  4010ac:	75 dd                	jne    40108b <phase_5+0x29>
  4010ae:	c6 44 24 16 00       	movb   $0x0,0x16(%rsp)//0x16(%rsp)在数组最后补上0
  4010b3:	be 5e 24 40 00       	mov    $0x40245e,%esi//取出的字符数组应为0x40245e: flyers
  4010b8:	48 8d 7c 24 10       	lea    0x10(%rsp),%rdi//0x10(%rsp)为取出的字符组成的数组
  4010bd:	e8 76 02 00 00       	callq  401338 <strings_not_equal>
  4010c2:	85 c0                	test   %eax,%eax
  4010c4:	74 13                	je     4010d9 <phase_5+0x77>
  4010c6:	e8 6f 03 00 00       	callq  40143a <explode_bomb>
  4010cb:	0f 1f 44 00 00       	nopl   0x0(%rax,%rax,1)
  4010d0:	eb 07                	jmp    4010d9 <phase_5+0x77>
  4010d2:	b8 00 00 00 00       	mov    $0x0,%eax
  4010d7:	eb b2                	jmp    40108b <phase_5+0x29>
  4010d9:	48 8b 44 24 18       	mov    0x18(%rsp),%rax
  4010de:	64 48 33 04 25 28 00 	xor    %fs:0x28,%rax
  4010e5:	00 00 
  4010e7:	74 05                	je     4010ee <phase_5+0x8c>
  4010e9:	e8 42 fa ff ff       	callq  400b30 <__stack_chk_fail@plt>
  4010ee:	48 83 c4 20          	add    $0x20,%rsp
  4010f2:	5b                   	pop    %rbx
  4010f3:	c3                   	retq   
  ```

程序思想是输入6个字符，以6个字符的低四位当做一个大小为16的字符数组的索引，从字符数组中取出6个字符，这6个字符依次是flyers(0x40245e地址处的字符串)。

gdb打印 **0x4024b0** 和 **0x40245e** 处字符串。

![phase5_prompt](/static/phase5_prompt.png)

f在字符数组 **maduiersnfotvbyl** 下标为9，查ascii码表，低4位为9的字符有), I, i。因此输入的第一个字符从三个中选一个即可。同理，其余字符列表如下：

| 字符  | 索引  | 解    |
| :---: | :---: | :---: |
| f     | 9     | ) I i |
| l     | f     | / O o |
| y     | e     | . N n |
| e     | 5     | % E e |
| r     | 6     | & F f |
| s     | 7     | ' G g |


## phase6

第六个炸弹代码有点长。

```
00000000004010f4 <phase_6>:
  4010f4:	41 56                	push   %r14
  4010f6:	41 55                	push   %r13
  4010f8:	41 54                	push   %r12
  4010fa:	55                   	push   %rbp
  4010fb:	53                   	push   %rbx
  4010fc:	48 83 ec 50          	sub    $0x50,%rsp
  401100:	49 89 e5             	mov    %rsp,%r13
  401103:	48 89 e6             	mov    %rsp,%rsi
  401106:	e8 51 03 00 00       	callq  40145c <read_six_numbers>
  40110b:	49 89 e6             	mov    %rsp,%r14
  40110e:	41 bc 00 00 00 00    	mov    $0x0,%r12d
//外循环开始↓
  401114:	4c 89 ed             	mov    %r13,%rbp
  401117:	41 8b 45 00          	mov    0x0(%r13),%eax
  40111b:	83 e8 01             	sub    $0x1,%eax
  40111e:	83 f8 05             	cmp    $0x5,%eax//每一个数都必须小于等于6
  401121:	76 05                	jbe    401128 <phase_6+0x34>
  401123:	e8 12 03 00 00       	callq  40143a <explode_bomb>
  401128:	41 83 c4 01          	add    $0x1,%r12d
  40112c:	41 83 fc 06          	cmp    $0x6,%r12d
  401130:	74 21                	je     401153 <phase_6+0x5f>//外循环结束判断
  401132:	44 89 e3             	mov    %r12d,%ebx
//内循环开始↓
  401135:	48 63 c3             	movslq %ebx,%rax
  401138:	8b 04 84             	mov    (%rsp,%rax,4),%eax//取输入的数
  40113b:	39 45 00             	cmp    %eax,0x0(%rbp)
                                //每一个数都不能和后面的数一样，也就是说输入的6个数不能重复
  40113e:	75 05                	jne    401145 <phase_6+0x51>
  401140:	e8 f5 02 00 00       	callq  40143a <explode_bomb>
  401145:	83 c3 01             	add    $0x1,%ebx
  401148:	83 fb 05             	cmp    $0x5,%ebx//输入没有结束
  40114b:	7e e8                	jle    401135 <phase_6+0x41>//内循环退出判断语句
//内循环结束↑
  40114d:	49 83 c5 04          	add    $0x4,%r13
  401151:	eb c1                	jmp    401114 <phase_6+0x20>
//外循环结束↑
  401153:	48 8d 74 24 18       	lea    0x18(%rsp),%rsi
  401158:	4c 89 f0             	mov    %r14,%rax
  40115b:	b9 07 00 00 00       	mov    $0x7,%ecx
//对输入的数用7取补，如果输入1则变成6,2变成5.↓
  401160:	89 ca                	mov    %ecx,%edx//对7取补
  401162:	2b 10                	sub    (%rax),%edx
  401164:	89 10                	mov    %edx,(%rax)
  401166:	48 83 c0 04          	add    $0x4,%rax
  40116a:	48 39 f0             	cmp    %rsi,%rax
  40116d:	75 f1                	jne    401160 <phase_6+0x6c>
//取补结束↑
  40116f:	be 00 00 00 00       	mov    $0x0,%esi
  401174:	eb 21                	jmp    401197 <phase_6+0xa3>

  401176:	48 8b 52 08          	mov    0x8(%rdx),%rdx
  40117a:	83 c0 01             	add    $0x1,%eax
  40117d:	39 c8                	cmp    %ecx,%eax
  40117f:	75 f5                	jne    401176 <phase_6+0x82>
  401181:	eb 05                	jmp    401188 <phase_6+0x94>

  401183:	ba d0 32 60 00       	mov    $0x6032d0,%edx//0x6032d0为链表首地址
  401188:	48 89 54 74 20       	mov    %rdx,0x20(%rsp,%rsi,2)
  40118d:	48 83 c6 04          	add    $0x4,%rsi
  401191:	48 83 fe 18          	cmp    $0x18,%rsi
  401195:	74 14                	je     4011ab <phase_6+0xb7>
  401197:	8b 0c 34             	mov    (%rsp,%rsi,1),%ecx//ecx依次取逆转之后的数
  40119a:	83 f9 01             	cmp    $0x1,%ecx
  40119d:	7e e4                	jle    401183 <phase_6+0x8f>

  40119f:	b8 01 00 00 00       	mov    $0x1,%eax
  4011a4:	ba d0 32 60 00       	mov    $0x6032d0,%edx
  4011a9:	eb cb                	jmp    401176 <phase_6+0x82>
  4011ab:	48 8b 5c 24 20       	mov    0x20(%rsp),%rbx
  4011b0:	48 8d 44 24 28       	lea    0x28(%rsp),%rax
  4011b5:	48 8d 74 24 50       	lea    0x50(%rsp),%rsi
  4011ba:	48 89 d9             	mov    %rbx,%rcx
  4011bd:	48 8b 10             	mov    (%rax),%rdx
  4011c0:	48 89 51 08          	mov    %rdx,0x8(%rcx)
  4011c4:	48 83 c0 08          	add    $0x8,%rax
  4011c8:	48 39 f0             	cmp    %rsi,%rax
  4011cb:	74 05                	je     4011d2 <phase_6+0xde>
  4011cd:	48 89 d1             	mov    %rdx,%rcx
  4011d0:	eb eb                	jmp    4011bd <phase_6+0xc9>
  4011d2:	48 c7 42 08 00 00 00 	movq   $0x0,0x8(%rdx)
  4011d9:	00 
  4011da:	bd 05 00 00 00       	mov    $0x5,%ebp
  4011df:	48 8b 43 08          	mov    0x8(%rbx),%rax
  4011e3:	8b 00                	mov    (%rax),%eax
  4011e5:	39 03                	cmp    %eax,(%rbx)
  4011e7:	7d 05                	jge    4011ee <phase_6+0xfa>//从大到小排序
  4011e9:	e8 4c 02 00 00       	callq  40143a <explode_bomb>
  4011ee:	48 8b 5b 08          	mov    0x8(%rbx),%rbx
  4011f2:	83 ed 01             	sub    $0x1,%ebp
  4011f5:	75 e8                	jne    4011df <phase_6+0xeb>
  4011f7:	48 83 c4 50          	add    $0x50,%rsp
  4011fb:	5b                   	pop    %rbx
  4011fc:	5d                   	pop    %rbp
  4011fd:	41 5c                	pop    %r12
  4011ff:	41 5d                	pop    %r13
  401201:	41 5e                	pop    %r14
  401203:	c3                   	retq   
```

这段代码分析起来有点复杂，简单说说流程。

**401114** 到 **401151** 的循环用于判断输入的6个数是不是都不一样且为1~6。因此输入为1,2,3,4,5,6的一个排序。

**401160** 到 **40116d** 的代码用于对7取补，取补的目的是为了混淆视听，增加难度。

**0x6032d0** 这个地址很重要。使用gdb打印前24个字：

![link_list](/static/link_list.png)

从结果来看，可以知道这是一个链表。每个结点大小为16字节，前4个字节为结点值，第二个为结点编号，第三个为指向下一结点的指针的低32位，第四个为指向下一结点的指针的高32位（没有用到高32位地址，所以均为0）。

从指针域来看，链表结点按编号有序。

程序会将链表按照输入的6个数进行排序，例如输入1,2,3,4,5,6，代码取补之后变成6,5,4,3,2,1。意思就是将6号结点变成链表的第一个结点，5号第二个，依次类比。

而 **4011e7** 处的指令对链表值进行从大到小的判断，也就是说我们输入的数要将链表按结点值从大到小排序。

根据图中结点值来看，0x39c>0x2b3>0x1dd>0x1bb>0x14c>0x0a8,对应结点编号就是3,4,5,6,1,2。由于程序有一个取补的过程，因此真正需要输入的是4,3,2,1,6,5。

![phase6_defused](/static/phase6_defused.png)


## secret_phase

(如何进入secret_phase请google)

secret phase代码如下。

```
0000000000401242 <secret_phase>:
  401242:	53                   	push   %rbx
  401243:	e8 56 02 00 00       	callq  40149e <read_line>
  401248:	ba 0a 00 00 00       	mov    $0xa,%edx
  40124d:	be 00 00 00 00       	mov    $0x0,%esi
  401252:	48 89 c7             	mov    %rax,%rdi
  401255:	e8 76 f9 ff ff       	callq  400bd0 <strtol@plt>
  40125a:	48 89 c3             	mov    %rax,%rbx//输入的数存放在rax中
  40125d:	8d 40 ff             	lea    -0x1(%rax),%eax
  401260:	3d e8 03 00 00       	cmp    $0x3e8,%eax//输入的数减1后小于或等于0x3e8
  401265:	76 05                	jbe    40126c <secret_phase+0x2a>
  401267:	e8 ce 01 00 00       	callq  40143a <explode_bomb>
  40126c:	89 de                	mov    %ebx,%esi
  40126e:	bf f0 30 60 00       	mov    $0x6030f0,%edi
  401273:	e8 8c ff ff ff       	callq  401204 <fun7>
  401278:	83 f8 02             	cmp    $0x2,%eax//fun7必须返回2
  40127b:	74 05                	je     401282 <secret_phase+0x40>
  40127d:	e8 b8 01 00 00       	callq  40143a <explode_bomb>
  401282:	bf 38 24 40 00       	mov    $0x402438,%edi
  401287:	e8 84 f8 ff ff       	callq  400b10 <puts@plt>
  40128c:	e8 33 03 00 00       	callq  4015c4 <phase_defused>
  401291:	5b                   	pop    %rbx
  401292:	c3                   	retq   
```

从 **401255** 处判断输入是一个数，程序会以字符串形式读取再使用strtol函数转换成long类型。

输入的数存放在rax寄存器中，接着判断输入的数减1后是否小于等于0x3e8。

然后输入的数放在esi中，edi存放了一个地址，**0x6030f0** 。调用fun7。**要求fun7返回时，eax等于2** 。

```
0000000000401204 <fun7>:
  401204:	48 83 ec 08          	sub    $0x8,%rsp
  401208:	48 85 ff             	test   %rdi,%rdi
  40120b:	74 2b                	je     401238 <fun7+0x34>
  40120d:	8b 17                	mov    (%rdi),%edx
  40120f:	39 f2                	cmp    %esi,%edx
  401211:	7e 0d                	jle    401220 <fun7+0x1c>
  401213:	48 8b 7f 08          	mov    0x8(%rdi),%rdi //使rdi指向的数变小
  401217:	e8 e8 ff ff ff       	callq  401204 <fun7>
  40121c:	01 c0                	add    %eax,%eax//eax乘以2返回
  40121e:	eb 1d                	jmp    40123d <fun7+0x39>
  401220:	b8 00 00 00 00       	mov    $0x0,%eax//eax赋0并判断edx和输入数是否相等，相等返回。
  401225:	39 f2                	cmp    %esi,%edx
  401227:	74 14                	je     40123d <fun7+0x39>
  401229:	48 8b 7f 10          	mov    0x10(%rdi),%rdi //使rdi指向的数变大
  40122d:	e8 d2 ff ff ff       	callq  401204 <fun7>
  401232:	8d 44 00 01          	lea    0x1(%rax,%rax,1),%eax//eax乘以2加1返回
  401236:	eb 05                	jmp    40123d <fun7+0x39>
  401238:	b8 ff ff ff ff       	mov    $0xffffffff,%eax
  40123d:	48 83 c4 08          	add    $0x8,%rsp
  401241:	c3                   	retq   
```

fun7过程如下（esi为输入的数）：
- 将rdi指向的数取到edx中。
- 当edx等于esi时，eax赋0并返回。
- 当edx小于esi时，更新rdi指针`mov 0x10(%rdi),%rdi`(使rdi指向的数变大)，递归调用fun7，返回时eax乘以2再加1。
- 当edx大于esi时，更新rdi指针`mov 0x8(%rdi),%rdi`(使rdi指向的数变小)，递归调用fun7，返回时eax乘以2。

要想fun7返回secret_phase时eax等于2，可以使edx先大于esi，这时递归调用fun7，然后使edx小于esi，再次递归调用fun7，此时使edx=esi，fun7将eax置为0并返回到第二层fun7中，然后eax=eax×2+1=1，接着返回到第一层fun7中，eax=eax×2=2，最后返回到secret_phase中，eax=2，炸弹拆除。

现在的问题就是如何选取esi能达到刚才的要求。gdb打印 **0x6030f0** 处的内容如下：

![secret_phase](/static/secret_phase.png)

第一次在 **40126e** 处将 **0x6030f0** 送到edi(rdi的低32位)中，fun7地址 **40120d** 处的指令将rdi指向的数送到edx中，根据图中内容来看，edx为0x24。由于要求第一次edx先大于esi，所以esi小于0x24。接着 **401213** 处指令将rdi+8处内容送到rdi中，rdi变成 **0x603110** ，递归调用fun7。

这时rdi指向内容变成 **0x603110** 处的 **0x8** ，edx变为 **0x8** ，要求edx小于esi，所以esi大于 **0x8**，接着 **401229** 处指令将rdi+16处内容送到rdi中，rdi变成 **0x603150** ，递归调用fun7。

这时rdi指向内容变成 **0x603150** 处的 **0x16** ，edx变为 **0x16** ，要求edx等于esi，所以esi等于 **0x16**。

答案即为0x16(22)。

![secret_phase_defused](/static/secret_phase_defused.png)